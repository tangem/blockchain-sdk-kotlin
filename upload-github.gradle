apply plugin: 'maven-publish'

def publishPropsFile = rootProject.file("publish.properties")
def publishProps = new Properties()
if (publishPropsFile.exists()) {
    publishProps.load(new FileInputStream(publishPropsFile))
}

ext {
    artifactGroupId = findProperty('artifactGroupId') ?: publishProps.getProperty('artifactGroupId')
    artifactId = findProperty('artifactId') ?: publishProps.getProperty('artifactId')
    artifactVersion = findProperty('artifactVersion') ?: publishProps.getProperty('artifactVersion')
}

afterEvaluate {
    publishing {

        publications {
            bar(MavenPublication) {
                groupId = project.ext.artifactGroupId
                artifactId = project.ext.artifactId
                version = project.ext.artifactVersion

                from components.release
                // artifact(sourceJar)

                pom.withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
                        it.groupId.text() == project.artifactGroupId
                    }.each() {
                        it.parent().remove(it)
                    }
                    pomNode.dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime'
                    }.each {
                        it.scope*.value = 'compile'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/tangem/blockchain-sdk-kotlin")
                credentials {
                    username = findProperty("githubUser")
                    password = findProperty("githubPass")
                }
            }
        }
    }
}
